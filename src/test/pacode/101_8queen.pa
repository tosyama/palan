ccall printf(...);

const X,Y = 8,8;
int32 count;
[Y,X]byte board;

board ->> initBoard()
	->> queen(-1, -1)
	->> board, count;

printf("answer: %d\n", count);

func initBoard([Y,X]byte >>board)
	-> [Y,X]byte board
{
	y = 0;
	while y < Y {
		x = 0;
		while x < X {
			0->board[y, x];
			x++;
		}
		y++;
	}
}

func queen([Y,X]byte >>board, int32 x, y)
	-> [Y,X]byte board, int32 count
{
	0 -> count;

	{
		xx = x-1;
		while xx >= 0 {
			if board[y, xx] { return }
			xx-1 -> xx;
		}
	}

	{
		xx = x-1;
		yy = y-1;
		while xx >= 0 && yy >= 0 {
			if board[yy, xx] { return }
			xx-1, yy-1 -> xx, yy;
		}
	}

	{
		xx = x-1;
		yy = y+1;
		while xx >= 0 && yy < 8 {
			if board[yy, xx] { return }
			xx-1 -> xx;
			yy++;
		}
	}

	if x >= 0 { 1 -> board[y, x] }

	if x == X-1 {
		count++;
	} else {
		yy = 0;
		while yy < Y {
			int32 cnt;
			board ->> queen(x+1, yy)
				->> board,  cnt;
			cnt + count -> count;
			yy++;
		}
	}

	if x >= 0 { 0 -> board[y, x] }
}
